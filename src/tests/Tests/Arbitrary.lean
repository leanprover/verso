/-
Copyright (c) 2025 Lean FRO LLC. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: David Thrane Christiansen
-/
module
public import Plausible
public import Plausible.ArbitraryFueled
public meta import Plausible.ArbitraryFueled
import Lean.Data.Json.FromToJson
import all MultiVerso.InternalId
public meta import MultiVerso.NameMap
public meta import MultiVerso
public meta import VersoManual.Html.JsFile
public meta import VersoManual.Html.CssFile
public meta import VersoManual.Html.Features
public meta import VersoManual.LicenseInfo
public meta import VersoSearch.DomainSearch
public meta import Verso.Output.Html
public meta import MultiVerso.Manifest
public meta import VersoManual.Basic
import all VersoManual.Basic
import VersoManual.Html.CssFile

open Lean
open Plausible Gen Arbitrary
open Verso Multi
open Shrinkable
open Std

/-!
This module contains Plausible generators for most of the types that Verso regularly serializes or
deserializes.
-/


public meta section

def sizedArrayOf (gen : Gen Œ±) : Gen (Array Œ±) := do
  let count ‚Üê chooseNat
  let mut out := #[]
  for _ in 0...count do
    out := out.push (‚Üê gen.resize (¬∑ / count))
  return out

instance : Arbitrary InternalId where
  arbitrary := private InternalId.mk <$> arbitrary

instance : Shrinkable InternalId where
  shrink private
    | { id } => Shrinkable.shrink id <&> InternalId.mk


instance : Arbitrary JsonNumber where
  arbitrary := do
    return { mantissa := ‚Üê arbitrary, exponent := ‚Üê arbitrary }

instance : Shrinkable JsonNumber where
  shrink x :=
    let ms := shrink x.mantissa
    let xs := shrink x.exponent
    ms.map ({ x with mantissa := ¬∑ }) ++ xs.map ({ x with exponent := ¬∑ })


instance : ArbitraryFueled Json where
  arbitraryFueled := arb
where
  arb
    | 0 =>
      oneOf #[
        pure .null,
        pure (.bool true),
        pure (.bool false),
        .num <$> arbitrary,
        .str <$> arbitrary
      ]
    | n + 1 => do
      oneOf #[
        pure .null,
        pure (.bool true),
        pure (.bool false),
        .num <$> arbitrary,
        .str <$> arbitrary,
        .arr <$> sizedArrayOf (arb n),
        .obj <$> (Std.TreeMap.Raw.ofArray ¬∑ _) <$> genObj n
      ] (by simp)
  genObj (fuel : Nat) : Gen (Array (String √ó Json)) := do
    let count ‚Üê Gen.chooseNat
    let mut xs := #[]
    for _ in 0...count do
      xs := xs.push (‚Üê arbitrary, (‚Üê Gen.resize (¬∑ / count) (arb fuel)))
    return xs

partial instance : Shrinkable Json where
  shrink v := (if v matches .null then [] else [.null]) ++ sh v
where
  sh
    | .null => []
    | .bool true => [.bool false]
    | .bool _ => []
    | .num n => .num <$> shrink n
    | .str s => .str <$> shrink s
    | .arr xs =>
      have : Shrinkable Json := ‚ü®sh‚ü©
      .arr <$> shrink xs
    | .obj v =>
      have : Shrinkable Json := ‚ü®sh‚ü©
      let xs := v.toArray
      let xs' := shrink xs
      xs'.map fun v => .obj (Std.TreeMap.Raw.ofArray v _)

local instance : Arbitrary (Std.HashSet InternalId) where
  arbitrary := do
    return .ofArray (‚Üê Gen.arrayOf arbitrary)

local instance : Shrinkable (Std.HashSet InternalId) where
  shrink v :=
    Std.HashSet.ofArray <$> shrink v.toArray

instance : Arbitrary Object where
  arbitrary := do
    let canonicalName ‚Üê arbitrary
    let data ‚Üê arbitrary
    let ids ‚Üê arbitrary
    return { canonicalName, data, ids}

instance : Shrinkable Object where
  shrink x :=
    (shrink x.canonicalName |>.map ({x with canonicalName := ¬∑})) ++
    (shrink x.data |>.map ({ x with data := ¬∑ })) ++
    (shrink x.ids |>.map ({ x with ids := ¬∑ }))


instance : Arbitrary Domain where
  arbitrary := do
    let objects : Std.TreeMap String Object compare ‚Üê do
      let mut arr := #[]
      let count ‚Üê chooseNat
      for _ in 0...count do
        arr := arr.push (‚Üê arbitrary, ‚Üê arbitrary)
      pure (.ofArray arr (cmp := compare))
    let objectsById := objects.foldl (init := {}) fun byId x obj =>
      obj.ids.fold (init := byId) fun byId id =>
        byId.alter id fun
          | none => some {x}
          | some xs => some (xs.insert x)
    let title ‚Üê arbitrary
    let description ‚Üê arbitrary
    return { objects, objectsById, title, description }

instance : Shrinkable (HashSet String) where
  shrink xs :=
    shrink xs.toArray |>.map .ofArray

instance : Shrinkable Domain where
  shrink dom :=
    (shrink dom.objects.toArray |>.map ({ dom with objects := .ofArray ¬∑ })) ++
    (shrink dom.objectsById.toArray |>.map ({ dom with objectsById := .ofArray ¬∑})) ++
    (shrink dom.title |>.map ({ dom with title := ¬∑ })) ++
    (shrink dom.description |>.map ({ dom with description := ¬∑ }))

@[expose]
def Letter := Char
deriving BEq, ToString, Repr, Ord, Hashable

def letters : Vector Char 26 := "abcdefghijklmnopqrstuvwxyz".toList.toArray.toVector

instance : Arbitrary Letter where
  arbitrary := do
    let ‚ü®i, ‚ü®_, h‚ü©‚ü© ‚Üê choose Nat 0 25 (by grind)
    return letters[i]'(by grind)

instance instShrinkableLetter : Shrinkable Letter where
  shrink c :=
    if let some i := letters.findFinIdx? (¬∑ == c) then
      letters.take i |>.toList
    else []

@[expose]
def LetterString := String
deriving BEq, ToString, Repr, Ord, Hashable

instance : Arbitrary LetterString where
  arbitrary := do
    let len ‚Üê chooseNat
    let mut s := ""
    for _ in 0...(len + 1) do
      s := s.push ((‚Üê arbitrary) : Letter)
    return s

instance : Shrinkable LetterString where
  shrink s :=
    if let some first := s.startPos.get? then
      let rest := s.drop 1
      [String.singleton first] ++ [rest.copy] ++
      (instShrinkableLetter.shrink first |>.map (String.singleton ¬∑ ++ rest))
    else []

def slugChars := Slug.validChars.toArray

def slugChar : Gen Char := do
  have : slugChars.size > 0 := by decide +native
  let ‚ü®i, ‚ü®_, h‚ü©‚ü© ‚Üê choose Nat 0 (slugChars.size - 1) (by simp)
  return slugChars[i]'(by grind)

def slugString : Gen String := do
  let len ‚Üê chooseNat
  let mut s := ""
  for _ in 0...(len + 1) do
    s := s.push (‚Üê slugChar)
  return s

instance : Arbitrary Slug where
  arbitrary := do
    let s : String ‚Üê slugString
    return s.sluggify

instance : Shrinkable Slug where
  shrink x :=
    shrink x.toString |>.map (¬∑.sluggify)

instance : Arbitrary RemoteLink where
  arbitrary := do
    let path ‚Üê arbitrary
    let htmlId ‚Üê arbitrary
    let root ‚Üê arbitrary
    return { path, htmlId, root}

instance : Shrinkable RemoteLink where
  shrink x :=
    (shrink x.path |>.map ({ x with path := ¬∑ })) ++
    (shrink x.htmlId |>.map ({ x with htmlId := ¬∑ })) ++
    (shrink x.root |>.map ({ x with root := ¬∑ }))


instance : Arbitrary RefObject where
  arbitrary := do
    let link ‚Üê arbitrary
    let data ‚Üê arbitrary
    return { link, data }

instance : Shrinkable RefObject where
  shrink x :=
    (shrink x.link |>.map ({ x with link := ¬∑ })) ++
    (shrink x.data |>.map ({ x with data := ¬∑ }))

instance : Arbitrary RefDomain where
  arbitrary := do
    let title ‚Üê arbitrary
    let description ‚Üê arbitrary
    let mut contents := {}
    let count ‚Üê chooseNat
    for _ in 0...count do
      contents := contents.insert (‚Üê arbitrary) (‚Üê Gen.resize (¬∑ / count) arbitrary)
    return { title, description, contents }

instance [Shrinkable Œ±] [Shrinkable Œ≤] [BEq Œ±] [Hashable Œ±] : Shrinkable (Std.HashMap Œ± Œ≤) where
  shrink xs :=
    shrink xs.toArray |>.map (Std.HashMap.insertMany {} ¬∑)

instance [Shrinkable Œ±] [BEq Œ±] [Hashable Œ±] : Shrinkable (Std.HashSet Œ±) where
  shrink xs :=
    shrink xs.toArray |>.map Std.HashSet.ofArray


instance : Shrinkable RefDomain where
  shrink x :=
    (shrink x.title |>.map ({ x with title := ¬∑})) ++
    (shrink x.description |>.map ({ x with description := ¬∑})) ++
    (shrink x.contents |>.map ({ x with contents := ¬∑}))

/-- Generates non-anonymous names that users could write -/
instance : Arbitrary Name where
  arbitrary := do
    let size ‚Üê frequency (pure 0) [(5, pure 0), (1, chooseNat)]
    let mut x : Name := .str .anonymous (‚Üê arbitrary)
    for _ in 0...size do
      x := .str x (‚Üê arbitrary)
    return x

def chars : Vector Char 74 :=
  "abcdefghijklmnopqrstuvwzyz√¶√∏√•.ABCDEFGHIJKLMNOPQRSTUVWXYZ√Ü√ò√ÖŒªùí´() `_+√ó‚äï¬∑‚ü®‚ü©[]".toList.toArray.toVector

private def nameComponent : Gen String := do
    let mut s := ""
    for _ in 0...(‚Üê chooseNat) do
      s := s.push (‚Üê ch)
    return s
where
  ch : Gen Char := do
    let ‚ü®i, ‚ü®_, h‚ü©‚ü© ‚Üê choose Nat 0 (chars.size - 1) (by grind)
    return chars[i]

instance : Arbitrary NameMap.PublicName where
  arbitrary := private do
    let size ‚Üê frequency (pure 0) [(5, pure 0), (1, chooseNat)]
    let mut x : NameMap.PublicName := .ofName <| .str .anonymous (‚Üê arbitrary)
    for _ in 0...size do
      x := ‚ü®.str x.toName (‚Üê nameComponent), by grind‚ü©
    return x


instance : Shrinkable NameMap.PublicName where
  shrink
    | ‚ü®.str .anonymous x, _‚ü© =>
      shrink x |>.map (.ofName <| .str .anonymous ¬∑)
    | ‚ü®.str y@(.str _ _) x, _‚ü© =>
      .ofName y ::
      (shrink x |>.map (.ofName <| .str y ¬∑))
    | ‚ü®.num _ _, _‚ü© | ‚ü®.anonymous, _‚ü© | ‚ü®.str (.num _ _) _, _‚ü© =>
      False.elim <| by grind

instance [Arbitrary Œ±] : Arbitrary (Verso.NameMap Œ±) where
  arbitrary := do
    let mut domains : Verso.NameMap Œ± := {}
    let count ‚Üê chooseNat
    for _ in 0...count do
      let (‚ü®x, ok‚ü© : NameMap.PublicName) ‚Üê arbitrary
      domains := domains.insert x (‚Üê Gen.resize (¬∑ / count) arbitrary) ok
    return domains

open Shrinkable in
instance [Shrinkable Œ±] : Shrinkable (Verso.NameMap Œ±) where
  shrink v :=
    shrink v.toArray |>.map fun v => .ofArray v _

instance : Arbitrary RemoteInfo where
  arbitrary := do
    let root ‚Üê arbitrary
    let shortName ‚Üê arbitrary
    let longName ‚Üê arbitrary
    let domains ‚Üê arbitrary
    return { root, shortName, longName, domains }

instance : Shrinkable Name where
  shrink
    | .anonymous => []
    | .num x y => [x] ++ (shrink y).map (.num x)
    | .str x y => [x] ++ (shrink y).map (.str x)

instance : Shrinkable RemoteInfo where
  shrink v :=
    (shrink v.root |>.map ({ v with root := ¬∑ })) ++
    (shrink v.shortName |>.map ({ v with shortName := ¬∑ })) ++
    (shrink v.longName |>.map ({ v with longName := ¬∑ })) ++
    (shrink v.domains |>.map ({ v with domains := ¬∑ }))

instance : Arbitrary AllRemotes where
  arbitrary := do
    let mut xs := {}
    let count ‚Üê chooseNat
    for _ in 0...count do
      xs := xs.insert (‚Üê arbitrary) (‚Üê Gen.resize (¬∑ / count) arbitrary)
    return ‚ü®xs‚ü©

instance : Shrinkable AllRemotes where
  shrink x :=
    shrink x.allRemotes |>.map AllRemotes.mk

private def pathChars : Vector Char 55 :=
  "abcdefghijklmnopqrstuvwzyzABCDEFGHIJKLMNOPQRSTUVWXYZ-._".toList.toArray.toVector

private def pathComp : Gen String := do
  let mut s := ""
  for _ in 0...(‚Üê chooseNat) do
    s := s.push (‚Üê ch)
  return s
where
  ch : Gen Char := do
    let ‚ü®i, ‚ü®_, h‚ü©‚ü© ‚Üê choose Nat 0 (pathChars.size - 1) (by grind)
    return pathChars[i]'(Nat.lt_succ_of_le h)

instance : Arbitrary System.FilePath where
  arbitrary := private do
    let components ‚Üê arrayOf pathComp
    let isRoot ‚Üê chooseAny Bool
    let init : System.FilePath := if isRoot then "/" else ""
    return components.foldl (Œ± := String) (init := init) fun x y => x / y

instance : Shrinkable System.FilePath where
  shrink path :=
    if let some parent := path.parent then
      parent :: (path.fileName.toList.flatMap shrink |>.map path.withFileName)
    else []
